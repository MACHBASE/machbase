---
title: SCRIPT - examples
type: docs
weight: 55
---

{{< callout emoji="ðŸ“Œ" >}}
For smooth practice, the following query should be run to prepare tables and data.
{{< /callout >}}

```sql
CREATE TAG TABLE IF NOT EXISTS EXAMPLE (
    NAME VARCHAR(20) PRIMARY KEY,
    TIME DATETIME BASETIME,
    VALUE DOUBLE SUMMARIZED
);
INSERT INTO EXAMPLE VALUES('TAG0', TO_DATE('2021-08-12'), 10);
INSERT INTO EXAMPLE VALUES('TAG1', TO_DATE('2021-08-13'), 11);
```

Supporting script language

*Syntax*: `SCRIPT({ ... script code... })`

{{< tabs items="tengo">}}

{{< tab >}}
[tengo](https://github.com/d5/tengo) is a Golang like script.
Supports all builtin pakcages(math, text, times, rand, fmt, json, base64, hex, os).
And added "context" package for exposing the TQL specific features.

{{< /tab >}}

{{< /tabs >}}

## Package `context`

Returns context object of the script runtime.

### yieldKey, yield

Pass the incoming arguments to the next function.

**Example**

```js
SCRIPT({
    ctx := import("context")
    ctx.yield(0, 1, 2, 3)
    ctx.yield(1, 2, 3, 4)
})
CSV()
```

`result`

```
0,1,2,3
1,2,3,4
```

**Example**

```js
SCRIPT({
    ctx := import("context")
    ctx.yield("tag0", 100, 10)
    ctx.yield("tag0", 111, 11)
})
APPEND(table('example'))
```

`result`

```
append 2 rows (success 2, fail 0).
```

### key

Returns the key of the current record.

```js
SQL(`select * from example`)
SCRIPT({
    ctx := import("context")
    ctx.yieldKey(ctx.key(), 0, 1, 2, 3)
})
CSV()
```

`result`

```
TAG0,0,1,2,3
TAG1,0,1,2,3
```

### value

Returns the whole value of the current records in array. If the index is given, it returns the element of the values.

For example, If the current value is `[0, true, "hello", "world"]`

- `value()` returns the whole value array `[0, true, "hello", "world"]`
- `value(0)` returns the first element of the value `0`
- `value(3)` returns the last element of the value `"world"`

```js
SQL(`select * from example`)
SCRIPT({
    ctx := import("context")
    ctx.yield(ctx.value(1), 0, 1, 2, 3)
})
CSV()
```

`result`

```
10,0,1,2,3
11,0,1,2,3
```

## Package `times`

TQL can be used to facilitate conversion between `Timestamp` and `Time format string`.

### Timestamp to Time format string

```js
STRING(param("format_time") ?? "808210800000000000", separator('\n'))
SCRIPT({
    ctx := import("context")
    times := import("times")
    text := import("text")

    epoch_txt := ctx.value()[0]
    epoch := text.parse_int(epoch_txt, 10, 64)
    epoch = epoch / 1000000000

    t_time := times.time_format(epoch, "Mon Jan 2 15:04:05 -0700 MST 2006")

    ctx.yield(epoch, t_time)
})
CSV()
```

`result`
```
808210800,Sat Aug 12 16:00:00 +0900 KST 1995
```

### Time format string to Timestamp

```js
STRING(param("timestamp") ?? "Sat Aug 12 00:00:00 -0700 MST 1995", separator('\n'))
SCRIPT({
    ctx := import("context")
    times := import("times")
    text := import("text")

    time_format := ctx.value()[0]
    epoch := times.parse("Mon Jan 2 15:04:05 -0700 MST 2006", time_format)

    ctx.yield(epoch, time_format)
})
CSV()
```

`result`

```
808210800000000000,Sat Aug 12 00:00:00 -0700 MST 1995
```

## Parse custom JSON

It is required to tranform the incoming JSON data that is sent by external system to process into machbase-neo. Using TQL script makes it possible without developing or modifying exsting applictions.

Prepare test data saved in "script-data.json".

```json
{
  "tag": "pump",
  "data": {
    "string": "Hello TQL?",
    "number": "123.456",
    "time": 1687405320,
    "boolean": true
  },
  "array": ["elements", 234.567, 345.678, false]
}
```

Copy the code below into TQL editor and save `script-post-json.tql`.

```js
BYTES(payload())
SCRIPT({
  json := import("json")
  ctx := import("context")
  val := ctx.value()
  obj := json.decode(val[0])
  // parse a value from json, yield multiple records
  ctx.yield(obj.tag+"_0", obj.data.time*1000000000, obj.data.number)
  ctx.yield(obj.tag+"_1", obj.data.time*1000000000, obj.array[1])
  ctx.yield(obj.tag+"_2", obj.data.time*1000000000, obj.array[2])
})
CSV()
```

Post the test data JSON to the tql.

```sh
curl -o - --data-binary @script-data.json http://127.0.0.1:5654/db/tql/script-post-json.tql
```

This example takes a json object via HTTP POST and decode with JSON decoder then produces records by  calling `context.yield()` multiple times.

```sh
$ curl -o - --data-binary @script-data.json http://127.0.0.1:5654/db/tql/script-post-json.tql
name-0,1687405320000000000,123.456
name-1,1687405320000000000,234.567000
name-2,1687405320000000000,345.678000
```

If the script ends with `APPEND(...)` or `INSERT(...)` instead of `CSV()` the final result records will be written into database.

## Parse custom text

Make test data in 'script-post-lines.txt'.

```
11111
22222
33333
44444
```

Copy the code below into TQL editor and save as `script-post-lines.tql`.
```js
// Produce a {key:lineno, value:string} record per line
STRING(payload(), separator('\n'))
SCRIPT({
  text := import("text")
  times := import("times")
  ctx := import("context")
  key := ctx.key()
  values := ctx.value()
  str := text.trim_space(values[0])
  if len(str) == 0 {
    ctx.drop() // ignore empty line
  } else { // parsing
    str = text.substr(str, 0, 2)
    ctx.yield(
      "text_"+key,                // new key
      times.now(),                // time
      text.parse_int(str, 10, 64) // convert to int
    )
  }
})
CSV()
```

Send the test data to the *tql* via HTTP POST.

```sh
curl -o - --data-binary @script-post-lines.txt http://127.0.0.1:5654/db/tql/script-post-lines.tql
```

```sh
$ curl -o - --data-binary @script-post-lines.txt http://127.0.0.1:5654/db/tql/script-post-lines.tql
text_0,1687476301286716000,11
text_1,1687476301286750000,22
text_2,1687476301286791000,33
text_3,1687476301286853000,44
```

If the script ends with `APPEND(...)` or `INSERT(...)` instead of `CSV()` the final result records will be written into database.

## User defined function

Open a new *tql* editor on the web ui and copy the code below and run it.

In this example, `linspace(-4,4,100)` generates an array contains 100 elements which are ranged from -4.0 to 4.0 in every `8/100` step. `meshgrid()` takes two array and produce meshed new array. As result of FAKE() in the example produces an array of 10000 elements (100 x 100 meshed) contains array of two float point numbers.
`SCRIPT()` function takes a code block which enclosed by `{` and `}` and run it for each record.
Users can takes the key and value of the records via `context.key()` and `context.value()` then yield transformed data via `context.yield()`.

```js
FAKE(meshgrid(linspace(-4,4,100), linspace(-4,4, 100)))
SCRIPT({
  math := import("math")
  // Define a custom function in the script
  calc := func(a, b) {
    return math.sin(math.pow(a, 2) + math.pow(b, 2)) /
           (math.pow(a, 2) + math.pow(b, 2))
  }
  // Receive values of the record from context
  ctx := import("context")
  values := ctx.value()
  x := values[0]
  y := values[1]
  z := calc(x, y)
  // Yield new value
  //  - yieldKey() build and passes new value with new key to the next step.
  //  - yeild() build and passes new value to the next step with the received key from previous step
  ctx.yield(x, y, z)
})
CHART_LINE3D(
  // chart size in HTML syntax
  size('1000px', '600px'),
  // width, height, depth grids in percentage
  gridSize(100,50,100),
  lineWidth(5), visualMap(-0.1, 1),
  // rotation speed in degree per sec.
  autoRotate(20)
)
```

![web-tql-script-wave](/images/web-tql-script-wave.gif)